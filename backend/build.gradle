plugins {
	id 'java'
	id 'org.springframework.boot' version '3.4.1'
	id 'io.spring.dependency-management' version '1.1.7'
}

group = 'com.imjjh'
version = '0.0.1-SNAPSHOT'
description = 'Dibs'

java {
	toolchain {
		languageVersion = JavaLanguageVersion.of(21)
	}
}

repositories {
	mavenCentral()
}

dependencies {
	implementation 'org.springframework.boot:spring-boot-starter-data-jpa'
	implementation 'org.springframework.boot:spring-boot-starter-security'
	implementation 'org.springframework.boot:spring-boot-starter-web'
	implementation 'com.fasterxml.jackson.core:jackson-databind'
	implementation 'org.springframework.boot:spring-boot-starter-validation'
//	developmentOnly 'org.springframework.boot:spring-boot-devtools'

	// Redis & Cache
	implementation 'org.springframework.boot:spring-boot-starter-data-redis'
	implementation 'org.springframework.boot:spring-boot-starter-cache'

	// mysql
	runtimeOnly 'com.mysql:mysql-connector-j'

	// 테스트 의존성
	testImplementation 'org.springframework.boot:spring-boot-starter-test'
	testImplementation 'org.springframework.security:spring-security-test'
	testRuntimeOnly 'org.junit.platform:junit-platform-launcher'

	// oauth2
	implementation 'org.springframework.boot:spring-boot-starter-oauth2-client'

	// jwt
	implementation 'io.jsonwebtoken:jjwt-api:0.11.5'
	runtimeOnly 'io.jsonwebtoken:jjwt-impl:0.11.5'
	runtimeOnly 'io.jsonwebtoken:jjwt-jackson:0.11.5'

	// lombok
	compileOnly 'org.projectlombok:lombok'
	annotationProcessor 'org.projectlombok:lombok'
	testCompileOnly 'org.projectlombok:lombok'
	testAnnotationProcessor 'org.projectlombok:lombok'

	// swagger
	implementation 'org.springdoc:springdoc-openapi-starter-webmvc-ui:2.8.5'

	// Querydsl
	implementation 'com.querydsl:querydsl-jpa:5.0.0:jakarta'
	// Querydsl Q-Type 생성을 위한 어노테이션 프로세서 추가
	annotationProcessor "com.querydsl:querydsl-apt:5.0.0:jakarta"
	annotationProcessor "jakarta.annotation:jakarta.annotation-api"
	annotationProcessor "jakarta.persistence:jakarta.persistence-api"

	// H2 데이터베이스 추가 (테스트 환경 전용)
	testImplementation 'com.h2database:h2'

	// S3
	implementation 'io.awspring.cloud:spring-cloud-aws-starter-s3:3.1.0'

	// MapStruct
    implementation 'org.mapstruct:mapstruct:1.6.3'
    annotationProcessor 'org.mapstruct:mapstruct-processor:1.6.3'
    annotationProcessor 'org.projectlombok:lombok-mapstruct-binding:0.2.0'

}



// 기본 테스트 (./gradlew test)
tasks.named('test') {
	useJUnitPlatform()
}

// 단위 테스트 (./gradlew unitTest)
tasks.register('unitTest', Test) {
	group = 'verification'
	description = 'Runs only unit tests (@Tag("unit"))'

	testClassesDirs = sourceSets.test.output.classesDirs
	classpath = sourceSets.test.runtimeClasspath

	useJUnitPlatform {
		includeTags 'unit'
	}
}

// 통합 테스트 (./gradlew integrationTest)
tasks.register('integrationTest', Test) {
	group = 'verification'
	description = 'Runs only integration tests (@Tag("integration"))'

	testClassesDirs = sourceSets.test.output.classesDirs
	classpath = sourceSets.test.runtimeClasspath

	useJUnitPlatform {
		includeTags 'integration'
	}
}

// 모든 테스트 태스크(test, unitTest, integrationTest)에 공통 적용
tasks.withType(Test){
	testLogging {
		// 통과, 스킵, 실패 이벤트를 콘솔에 출력
		events "passed", "skipped", "failed"

		// 실패 시 예외 내용 자세히 보기
		exceptionFormat "full"
	}
}

// 자바 소스 컴파일 시 UTF-8 사용
tasks.withType(JavaCompile){
	options.encoding = 'UTF-8'
}

// 테스트 실행 시 콘솔 출력 인코딩 UTF-8 사용
tasks.withType(Test){
	systemProperty 'file.encoding', 'UTF-8'
}


// Q-Type 생성 경로 설정
def generatedDir = 'src/main/generated'

sourceSets {
	main {
		java {
			srcDirs += [ generatedDir ]
		}
	}
}

tasks.withType(JavaCompile) {
	options.getGeneratedSourceOutputDirectory().set(file(generatedDir))
}

tasks.named('clean'){
	delete file(generatedDir)
}

